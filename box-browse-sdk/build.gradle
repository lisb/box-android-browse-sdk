plugins {
    id 'com.android.library'
    id 'maven-publish'
    // このプロジェクト本体ではKotlinを使ってないが、
    // 依存先のライブラリが利用していてそれらでKotlinのバージョンがコンフリクトしていてビルドできないため、
    // このプロジェクトでバージョンを指定
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace "com.box.androidsdk.browse"

    compileSdk rootProject.tools.compileSdk

    defaultConfig {
        minSdk rootProject.tools.minSdk
        targetSdk rootProject.tools.targetSdk

        versionName "${rootProject.tools.versionName}"

        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        // このプロジェクト本体ではKotlinを使ってないが、
        // 依存先のライブラリが利用していてそれらでKotlinのバージョンがコンフリクトしていてビルドできないため、
        // このプロジェクトでバージョンを指定
        jvmTarget = '1.8'
    }

    lintOptions {
        abortOnError false
    }

    publishing {
        publishing {
            singleVariant("release") {
                withSourcesJar()
                withJavadocJar()
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    api 'com.box:box-android-sdk:5.0.0'

    implementation "androidx.appcompat:appcompat:1.7.0"
    implementation "androidx.localbroadcastmanager:localbroadcastmanager:1.1.0"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation "androidx.recyclerview:recyclerview:1.4.0"
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/lisb/box-android-browse-sdk"
            credentials {
                username = gradle.localProps["gpr.user"] ?: System.getenv("MVN_GITHUB_PACKAGES_USERNAME")
                password = gradle.localProps["gpr.key"] ?: System.getenv("MVN_GITHUB_PACKAGES_TOKEN")
            }
        }
    }

    publications {
        release(MavenPublication) {
            groupId = "com.lisb.android.box"
            artifactId = "box-android-browse-sdk"
            version = "${rootProject.tools.versionName}"

            pom {
                name = "Android Box Browse SDK"
                description = "LisB Fork of Box Android Browse SDK ${rootProject.tools.versionName}"
                url = "https://github.com/lisb/box-android-browse-sdk"
                scm {
                    url = "https://github.com/lisb/box-android-browse-sdk"
                    connection = "scm:git@github.com:lisb/box-android-browse-sdk.git"
                }

                licenses {
                    license {
                        name = "The Apache Software License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }

                developers {
                    developer {
                        id = "lisb"
                        name = "L is B corp."
                    }
                }
            }

            afterEvaluate {
                from components.release
            }
        }
    }
}
