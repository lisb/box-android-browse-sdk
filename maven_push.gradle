apply plugin: 'maven-publish'

afterEvaluate {

    def localProps = new Properties()
    localProps.load(rootProject.file('local.properties').newDataInputStream())

    task androidJavadocs(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        classpath += files(android.libraryVariants.collect { variant -> variant.javaCompileProvider.get().classpath.files })
    }

    task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
        archiveClassifier.set('javadoc')
        from androidJavadocs.destinationDir
    }

    task androidSourcesJar(type: Jar) {
        archiveClassifier.set('sources')
        from android.sourceSets.main.java.srcDirs
    }

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/lisb/box-android-browse-sdk"
                credentials {
                    username = localProps["gpr.user"] ?: System.getenv("GPR_USER")
                    password = localProps["gpr.key"] ?: System.getenv("GPR_KEY")
                }
            }
        }
        publications {
            release(MavenPublication) {
                from components.release
                artifact tasks.androidSourcesJar
                artifact tasks.androidJavadocsJar
                groupId = "com.lisb.android.box"
                artifactId = POM_ARTIFACT_ID
                version = "3.0.3"

                pom {
                    name = POM_NAME
                    description = "LisB Fork of Box Android Browse SDK 3.0.2"
                    url = "https://github.com/lisb/box-android-browse-sdk"
                    scm {
                        url = "https://github.com/lisb/box-android-browse-sdk"
                        connection = "scm:git@github.com:lisb/box-android-browse-sdk.git"
                    }

                    licenses {
                        license {
                            name = "The Apache Software License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }

                    developers {
                        developer {
                            id = "lisb"
                            name = "L is B corp."
                        }
                    }
                }
            }
        }
    }
}
